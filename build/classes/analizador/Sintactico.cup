package analizador;

import java_cup.runtime.Symbol;
import java.util.ArrayList;
import java.util.Map;
import java.util.HashMap;

import analizador.ast.*;
import analizador.ast.entorno.*;
import analizador.ast.expresion.*;
import analizador.ast.instruccion.*;

parser code
{:
	private Ast ast;
	
	public Ast getAST(){
		return ast;
	}
	
    @Override
    public void syntax_error(Symbol s){
        System.out.println("Error Sintactico en la Linea " + (s.right+1) +" Columna "+(s.left+1)+ ". Identificador " +s.value + " no reconocido." );
		//AnalizadorError tmp = new AnalizadorError();
		//tmp.setColumna(s.right+1);
		//tmp.setLinea(s.left+1);
		//tmp.setValor(s.value+"");
		//tmp.setError("Sint√°ctico");
        
		//error.add(tmp);
    }

    @Override
    public void unrecovered_syntax_error(Symbol s){
        System.out.println("Error Sintactico Fatal en la Linea " + (s.right+1)+ " Columna "+(s.left+1)+". Identificador " + s.value + " no reconocido.");
		//report_fatal_error("Error sintactico: <"+s.value+">", cur_token);
	}
:}

action code
{:

:}

terminal String tstring, tchar, entero, decimal, id, null_, true_, false_;
terminal llaveIzquierda, llaveDerecha, parIzquierda, parDerecha, corcheteIzquierda, corcheteDerecha, puntoycoma, coma, punto;
terminal dospuntos, interrogacion;
terminal mas, menos, asterisco, diagonal, porcentaje, umenos, umas, masmas, menosmenos;
terminal mayorque, menorque, mayorigual, menorigual, igualigual, diferente;
terminal and, or, not, xor;
terminal igual, masigual, menosigual, porigual, diagonaligual;
terminal public_, protected_, private_, abstract_, static_, final_, extends_, class_;
terminal int_, double_, char_, boolean_;
terminal print_, println_;

non terminal INICIO;

non terminal Clase CLASE_DECLA;
non terminal Clase CLASE;
non terminal ArrayList<Modificador> MODIFICADORES;
non terminal Modificador MODIFICADOR;
non terminal ArrayList<Declaracion> CUERPO_CLASE;
non terminal ArrayList<Declaracion> CUERPO_CLASE_LISTA;
non terminal Declaracion CUERPO_CLASE_DEC;
non terminal Declaracion CUERPO_CLASE_DEC_MOD;
non terminal Campo CAMPO;
non terminal ArrayList<Asignacion> DECLARACION_VARIABLES;
non terminal Asignacion DECLARACION_VARIABLE;
non terminal Identificador DECLARACION_VARIABLE_ID;
non terminal Tipo TIPO; 
non terminal Expresion VALOR;

non terminal Expresion E;

precedence left interrogacion;
precedence left or;
precedence left and;
precedence right not;
precedence left igualigual;
precedence left diferente;
precedence left mayorque, menorque, mayorigual, menorigual;
precedence left mas, menos;
precedence left asterisco, diagonal;
precedence right porcentaje;
precedence right masmas, menosmenos;
precedence right umenos, umas;

start with INICIO;

INICIO ::= CLASE_DECLA:a
        {: 
            parser.ast = new Ast(a);
            System.out.println("COLINE OK"); 
        :};

CLASE_DECLA ::= MODIFICADORES:a CLASE:b {: RESULT = b; RESULT.setModificadores(a); :}
                | CLASE:a {: RESULT = a; :};

CLASE ::=  class_ id:a llaveIzquierda CUERPO_CLASE:b llaveDerecha 
            {: RESULT = new Clase(a, b, aleft, bright); :}
        | class_ id:a extends_ id:b llaveIzquierda CUERPO_CLASE:c llaveDerecha 
            {: RESULT = new Clase(a, b, c, aleft, aright); :};

MODIFICADORES ::= MODIFICADORES:a MODIFICADOR:b {:RESULT = a; RESULT.add(b); :}
                | MODIFICADOR:a {: RESULT = new ArrayList<>(); RESULT.add(a); :};

MODIFICADOR ::= public_ {: RESULT = Modificador.PUBLIC; :}
                | protected_ {: RESULT = Modificador.PROTECTED; :}
                | private_ {: RESULT = Modificador.PRIVATE; :}
                | abstract_ {: RESULT = Modificador.ABSTRACT; :}
                | static_ {: RESULT = Modificador.STATIC; :}
                | final_ {: RESULT = Modificador.FINAL; :};

CUERPO_CLASE ::= {: RESULT = null; :}
                | CUERPO_CLASE_LISTA:a {: RESULT = a; :};

CUERPO_CLASE_LISTA ::= CUERPO_CLASE_LISTA:a CUERPO_CLASE_DEC_MOD:b {: RESULT = a; RESULT.add(b); :}
                    | CUERPO_CLASE_DEC_MOD:a {: RESULT = new ArrayList<>(); RESULT.add(a); :};

CUERPO_CLASE_DEC_MOD ::= MODIFICADORES:a CUERPO_CLASE_DEC:b {: RESULT = b; RESULT.setModificadores(a); :}
                        | CUERPO_CLASE_DEC:a {: RESULT = a; :};

CUERPO_CLASE_DEC ::= CLASE:a {: RESULT = a; :} /*CONSTRUCTOR*/
                    |CAMPO:a {: RESULT = a; :};


CAMPO ::= TIPO:a DECLARACION_VARIABLES:b puntoycoma {: RESULT = new Campo(a, b, aleft, aright); :};

DECLARACION_VARIABLES ::= DECLARACION_VARIABLES:a coma DECLARACION_VARIABLE:b {: RESULT = a; RESULT.add(b); :}
                        |DECLARACION_VARIABLE:a {: RESULT = new ArrayList<>(); RESULT.add(a); :};

DECLARACION_VARIABLE ::= DECLARACION_VARIABLE_ID:a igual VALOR:b {: RESULT = new Asignacion(a,b); :}
                        | DECLARACION_VARIABLE_ID:a {: RESULT = new Asignacion(a); :};

DECLARACION_VARIABLE_ID ::= DECLARACION_VARIABLE_ID:a corcheteIzquierda corcheteDerecha{: RESULT = a; RESULT.addDimension(); :}
                            |id:a {: RESULT = new Identificador(a, aleft, aright); :};

TIPO ::= int_ {: RESULT = Tipo.INT; :}
        |double_ {: RESULT = Tipo.DOUBLE; :}
        |char_ {: RESULT = Tipo.CHAR; :}
        |boolean_ {: RESULT = Tipo.BOOLEAN; :}
        |id:a {: RESULT = Tipo.OBJECT; RESULT.setObject(a); :};

VALOR ::= E:e {: RESULT = e; :};

E ::= entero:a {: RESULT = new Literal(Tipo.INT, new Integer(a), aleft, aright); :}
    | decimal:a {: RESULT = new Literal(Tipo.DOUBLE, new Double(a), aleft, aright); :}
    | tstring:a {: RESULT = new Literal(Tipo.OBJECT, a, aleft, aright);  :}
    | tchar:a {: RESULT = new Literal(Tipo.CHAR, a.charAt(0), aleft, aright); :}
    | true_:a {: RESULT = new Literal(Tipo.BOOLEAN, new Boolean(true), aleft, aright); :}
    | false_:a {: RESULT = new Literal(Tipo.BOOLEAN, new Boolean(false), aleft, aright); :}
    | id:a {: RESULT = new Identificador(a, aleft, aright); :};

		
		
		